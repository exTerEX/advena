/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    MERGE_SP process
    Merges BLAST and HMM signature protein results.
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

process MERGE_SP {
    tag "${meta.id}"
    label "process_low"
    conda "${projectDir}/env/environment.yml"
    container "ghcr.io/exterex/icescreen-advena:1.3.3"

    input:
    tuple val(meta), path(blast_best), path(hmm_best)

    output:
    tuple val(meta), path("${meta.id}_detected_SP_source.tsv"), emit: merged
    path "versions.yml", emit: versions

    when:
    task.ext.when == null || task.ext.when

    script:
    """
    python3 ${params.icescreen_root}/icescreen_detection_SP/src/merge_SP_results.py \\
        --blastres ${blast_best} \\
        --hmmres ${hmm_best} \\
        -o ${meta.id}_detected_SP_source.tsv

    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        python: \$(python3 --version 2>&1 | sed 's/Python //')
        pandas: \$(python3 -c "import pandas; print(pandas.__version__)")
    END_VERSIONS
    """

    stub:
    """
    touch ${meta.id}_detected_SP_source.tsv

    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        python: 3.11.0
        pandas: 2.2.0
    END_VERSIONS
    """
}
